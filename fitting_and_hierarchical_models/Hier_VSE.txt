model{
  
  mu_theta ~ dnorm(0, 1)T(0,1)
  mu_delta ~ dnorm(0, 1)T(0,1)
  mu_a ~ dnorm(0,1)T(0,1)
  mu_phi ~ dnorm(0, 1)
  mu_C ~ dunif(0, 5)
  
  lambda_theta ~ dgamma(.01,.01)
  lambda_delta ~ dgamma(.01,.01)
  lambda_a ~ dgamma(.01,.01)
  lambda_phi ~ dgamma(.01,.01)
  lambda_C ~ dgamma(.01,.01)
  
  for (s in 1:nsubs){
  
    theta[s] ~ dnorm(mu_theta, lambda_theta)
    delta[s] ~ dnorm(mu_delta, lambda_delta)
    a[s] ~ dnorm(mu_a, lambda_a)
    phi[s] ~ dnorm(mu_phi, lambda_phi)
    C[s] ~ dnorm(mu_C, lambda_C)
  
    Exploit[s,1,1] <- 0
    Exploit[s,1,2] <- 0
    Exploit[s,1,3] <- 0
    Exploit[s,1,4] <- 0
    
    Explore[s,1,1] <- 0
    Explore[s,1,2] <- 0
    Explore[s,1,3] <- 0
    Explore[s,1,4] <- 0

  
    for (t in 2:ntrials[s]){
      
      v[s,t-1] <- r[s,t-1]^theta[s] - l[s,t-1]^theta[s]
      
      for (d in 1:4){
        
        Exploit[s,t,d] <- ifelse(x[s,t-1] == d, Exploit[s,t-1,d]*delta[s] + v[s,t-1], Exploit[s,t-1,d]*delta[s])
        
        Explore[s,t,d] <- ifelse(x[s,t-1] == d, 0, Explore[s,t-1, d]+ a[s]*(phi[s] - Explore[s,t-1, d]))
        
        exp_p[s,t,d] <- exp((Exploit[s,t,d] + Explore[s,t,d])*C[s])
      }
      
      for (d in 1:4){
        
        p[s,t,d] <- exp_p[s,t,d]/sum(exp_p[s,t,])
        
      }
      x[s,t] ~ dcat(p[s,t,])
    }
  }
}