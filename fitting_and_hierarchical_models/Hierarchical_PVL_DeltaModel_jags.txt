model{

  #group-level priors 
  mu_w ~dnorm(0,1)T(0,)
  mu_A ~dnorm(0,1)
  mu_theta~dnorm(0,1)T(0,)
  mu_a~dnorm(0,1)T(0,1)
  
  lambda_w ~dgamma(.01,.01)
  lambda_A ~dgamma(.01,.01)
  lambda_theta ~dgamma(.01,.01)
  lambda_a ~dgamma(.01,.01)
  
  for(s in 1:nsubs){
  
    #Inidivudla priors - drawn from group level distributions 
    w[s] ~ dnorm(mu_w,lambda_w)T(0,)
    A[s] ~ dnorm(mu_A, lambda_A) 
    theta[s] ~ dnorm(mu_theta, lambda_theta)T(0,)
    a[s] ~ dnorm(mu_a, lambda_a)T(0,1)
  
    #prior for the first Expected Value for each deck
    Ev[s,1,1] ~ dnorm(0,0.01) 
    Ev[s,1,2] ~ dnorm(0,0.01)
    Ev[s,1,3] ~ dnorm(0,0.01)
    Ev[s,1,4] ~ dnorm(0,0.01)
  
    #prior for which deck they choose at first round (we are deterministic and determine that they are equally prob)
    #p[s,1,1] <- .25
    #p[s,1,2] <- .25
    #p[s,1,3] <- .25
    #p[s,1,4] <- .25
  
    for (t in 2:ntrials[s]){
    
      for (d in 1:4){
      
        u[s,t,d] <- ifelse(X[s,t-1]<0, -w[s]*abs(X[s,t-1])^A[s], abs(X[s,t-1])^A[s]) 
        Ev_update[s,t,d] <- Ev[s,t-1,d]+ (a[s]*(u[s,t,d] - Ev[s,t-1,d]))  
        Ev[s,t,d] <- ifelse(x[s,t-1]==d, Ev_update[s,t,d], Ev[s,t-1,d])
        exp_p[s,t,d] <- exp(theta[s]*Ev[s,t,d])
      
      }
    
    for (d in 1:4){
      p[s,t,d] <- exp_p[s,t,d]/sum(exp_p[s,t,])
    }
    
    x[s,t] ~ dcat(p[s,t,]) 
  }
 }
}